
FUNCTION_BLOCK sablon

VAR_INPUT               // ulazne varijable
	severity : REAL;
	likelihood : REAL;
	company_size : REAL;
	continent : REAL;
	skills_required : REAL;
	company_sector : REAL;
END_VAR

VAR_OUTPUT              // izlazne varijable
	risk : REAL;
END_VAR

FUZZIFY severity
	TERM low := (0, 1) (30, 1) (100, 0);
	TERM medium := (0, 0) (60, 1) (100, 0);
	TERM high := (0, 0) (90, 1) (100, 1);
END_FUZZIFY

FUZZIFY likelihood
	TERM low := (0, 1) (30, 1) (100, 0);
	TERM medium := (0, 0) (60, 1) (100, 0);
	TERM high := (0, 0) (90, 1) (100, 1);
END_FUZZIFY

FUZZIFY skills_required
	TERM low := (0, 1) (30, 1) (100, 0);
	TERM medium := (0, 0) (60, 1) (100, 0);
	TERM high := (0, 0) (90, 1) (100, 1);
END_FUZZIFY

FUZZIFY company_size
	TERM do_dvesto_pedeset := (0, 1) (200, 1) (250, 0);
	TERM dvesto_pedeset_do_petsto := (0, 0) (250, 0) (260, 1) (450, 1) (500, 0);
	TERM petsto_do_hiljadu := (0, 0) (500, 0) (510, 1) (990, 1) (1000, 0);
	TERM hiljadu_preko := (0, 0) (1000, 0) (1100, 1);
END_FUZZIFY

FUZZIFY continent
	TERM europe := (0, 0) (10, 1) (15, 0);
	TERM asia := (15, 0) (20, 1) (25, 0);
	TERM africa := (25, 0) (30, 1) (35, 0);
	TERM north_america := (35, 0) (40, 1) (45, 0);
	TERM south_america := (45, 0) (50, 1) (55, 0);
	TERM australia := (55, 0) (60, 1) (65, 0);
END_FUZZIFY

FUZZIFY company_sector
	TERM hospital := (0, 0) (10, 1) (15, 0);
	TERM finance := (15, 0) (20, 1) (25, 0);
	TERM technology := (25, 0) (30, 1) (35, 0);
	TERM business_and_professional_service := (35, 0) (40, 1) (45, 0);
	TERM retail_and_manufacturing := (45, 0) (50, 1) (55, 0);
	TERM logistics_and_transportation := (55, 0) (60, 1) (65, 0);
END_FUZZIFY

DEFUZZIFY risk
	TERM low := (0, 1) (30, 1) (100, 0);
	TERM medium := (0, 0) (60, 1) (100, 0);
	TERM high := (0, 0) (90, 1) (100, 1);
	METHOD : COG;		// 'Center Of Gravity' odn. centar mase (koristi se i MM odn. mean max)
	DEFAULT := 0;		// podrazumevana vrednost (ukoliko nijedno pravilo nije aktivirano)
END_DEFUZZIFY

RULEBLOCK blok1
	AND : MIN;			// za konjunkciju se koristi minimum (samim tim sa za disjunkciju koristi maksimum)
	ACT : MIN;			// za zakljuƒçivanje (implikaciju) je izabran minimum (Mamdani implikacija) (osim MIN koristi se i PROD)
	ACCU : MAX;			// za kompoziciju je izabran maksimum (osim MAX koristi se i SUM)

	RULE 1 : IF severity IS high AND likelihood IS high THEN risk IS high;
	RULE 2 : IF likelihood IS high AND skills_required IS low THEN risk IS medium;
	RULE 3 : IF continent IS europe AND company_sector IS hospital THEN risk IS high WITH 0.7;
	RULE 4 : IF company_size IS hiljadu_preko AND likelihood IS high THEN risk IS high WITH 0.6;
	RULE 5 : IF severity IS low AND likelihood IS low THEN risk IS low;
	RULE 6 : IF severity IS medium AND likelihood IS low THEN risk IS low WITH 0.5;
	RULE 8 : IF continent IS north_america AND company_sector IS technology THEN risk IS high WITH 0.7;
	RULE 9 : IF likelihood IS medium AND company_sector IS technology AND continent IS north_america THEN risk IS high;
	RULE 10 : IF likelihood IS low AND severity IS high THEN risk IS medium WITH 0.8;
	RULE 11 : IF skills_required IS medium AND severity IS high AND likelihood IS high THEN risk IS high WITH 0.8;
	RULE 12 : IF company_sector IS finance AND company_size IS petsto_do_hiljadu THEN risk IS high WITH 0.7;
	RULE 13 : IF skills_required IS low AND company_size IS petsto_do_hiljadu AND likelihood IS low THEN risk IS low WITH 0.6;
	RULE 14 : IF skills_required IS high AND severity IS medium THEN risk IS medium WITH 0.7;
	RULE 15 : IF skills_required IS medium AND likelihood IS medium AND company_sector IS logistics_and_transportation THEN risk IS medium;
	RULE 16 : IF company_sector IS technology AND company_size IS petsto_do_hiljadu AND continent is europe AND likelihood IS high THEN risk IS high WITH 0.8;
	RULE 17 : IF continent IS africa AND company_size IS do_dvesto_pedeset THEN risk IS low WITH 0.6;
	RULE 18 : IF severity IS low AND skills_required IS medium AND likelihood IS medium THEN risk IS medium WITH 0.6;
	RULE 19 : IF skills_required IS high AND likelihood IS medium AND company_sector IS technology AND severity IS high THEN risk IS high;
    RULE 20 : IF severity IS high AND company_sector IS technology AND company_size IS petsto_do_hiljadu THEN risk IS high with 0.7;
END_RULEBLOCK

END_FUNCTION_BLOCK

